[Setup]
AppName=Allmydata 3.0
; AppVerName=Allmydata 3.0 (build %(major)d.%(minor)d.%(point)d-r%(revision)d)
AppVerName=Allmydata 3.0
AppVersion=%(major)d.%(minor)d.%(point)d-r%(revision)d
VersionInfoVersion=%(major)d.%(minor)d.%(point)d.%(revision)d
AppPublisher=Allmydata Inc.
AppCopyright=Copyright (c) 2004-2008 Allmydata, Inc.
AppPublisherURL=http://www.allmydata.com/
AppSupportURL=http://support.allmydata.com/
WizardSmallImageFile=../installer.bmp
DefaultDirName={pf}\Allmydata 3.0
DefaultGroupName=Allmydata 3.0
; minumum version NT 4, no classic windows
MinVersion=0,4.0
Compression=lzma/max
SolidCompression=yes
OutputDir=./installer/
SourceDir=dist
SetupIconFile=../installer.ico
UninstallDisplayIcon=../amdicon.ico
; license file needs to be build/all dir
;LicenseFile=../license.txt
OutputBaseFilename=Allmydata_Tahoe_Setup_v%(major)d_%(minor)d_%(point)d_r%(revision)d

[Files]
; contents of 'binaries' dir. (consolidated build target)
Source: "*.*"; DestDir: "{app}\Install"; Flags: restartreplace replacesameversion uninsrestartdelete
Source: ".\pkg_resources\*.*"; DestDir: "{app}\Install\pkg_resources"; Flags: recursesubdirs
Source: ".\winfuse\*.*"; DestDir: "{app}\Install\winfuse"; Flags: recursesubdirs

[Dirs]
Name: "{app}\noderoot"

[Icons]
; Program files entries
Name: "{group}\Allmydata"; Filename: "{app}\Install\winfuse\AllmydataTray.exe"
Name: "{commonstartup}\Allmydata"; Filename: "{app}\Install\winfuse\AllmydataTray.exe"
;Name: "{group}\Tahoe root dir (web)"; Filename: "{app}\Install\tahoe.exe"; Parameters: "webopen"
Name: "{group}\Allmydata Help"; Filename: "http://www.allmydata.com/help"

[Run]
; Things performed before the final page of the installer
Filename: "{sys}\net.exe"; Parameters: "stop ""Allmydata SMB"""; Flags: runhidden
Filename: "{sys}\net.exe"; Parameters: "stop Tahoe"; Flags: runhidden
Filename: "{app}\Install\tahoesvc.exe"; Parameters: "-install -auto"; Flags: runhidden
Filename: "{app}\Install\tahoe.exe"; Parameters: "create-client ""{app}\noderoot"""; Flags: runhidden
Filename: "{app}\Install\winfuse\InstallUtil.exe"; Parameters: """{app}\Install\winfuse\WinFUSE.exe"""; Flags: runhidden
Filename: "{app}\Install\confwiz.exe"; Flags: hidewizard
;Filename: "{app}\Install\ReadMe.txt"; Description: "View the ReadMe file"; Flags: unchecked postinstall nowait shellexec skipifdoesntexist
Filename: "{app}\Install\winfuse\AllmydataTray.exe"; Description: "Run Allmydata"; Flags: postinstall nowait
Filename: "http://beta.allmydata.com/welcome_install?v=%(major)d.%(minor)d.%(point)d.%(revision)d"; Description: "View the Welcome Page"; Flags: postinstall shellexec

[UninstallRun]
; Performed before the uninstaller runs to undo things
; xFilename: "{app}\Install\winfuse\AllmydataTray.exe"; Parameters: "-Q"; Flags: runhidden
Filename: "{sys}\net.exe"; Parameters: "stop ""Allmydata SMB"""; Flags: runhidden
Filename: "{sys}\net.exe"; Parameters: "stop Tahoe"; Flags: runhidden
Filename: "{app}\Install\winfuse\InstallUtil.exe"; Parameters: "/uninstall ""{app}\Install\winfuse\WinFUSE.exe"""; Flags: runhidden
Filename: "{app}\Install\tahoesvc.exe"; Parameters: "-remove"; Flags: runhidden
;Filename: "{app}\Install\confwiz.exe"; Parameters: "--uninstall"; Flags: runhidden
;Filename: "http://www.allmydata.com/redirect/uninstallsurvey.php?build=%(build)s"; Flags: shellexec

[Registry]
Root: HKLM; Subkey: "Software\Allmydata"; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: "Software\Allmydata"; ValueType: string; ValueName: "Base Dir Path"; ValueData: "{app}\noderoot"; Flags: uninsdeletekey
